<!DOCTYPE html>
<html>
<head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
<title>Re: Poison Cookie, Moldy Chocolate, and Chomp (was Gale&#39;s Game)</title>
<link rel="important stylesheet" href="">
<style>div.headerdisplayname {font-weight:bold;}
</style></head>
<body>
<table border=0 cellspacing=0 cellpadding=0 width="100%" class="moz-header-part1 moz-main-header"><tr><td><b>Subject: </b>Re: Poison Cookie, Moldy Chocolate, and Chomp (was Gale&#39;s Game)</td></tr><tr><td><b>From: </b>mcuddy@fensende.Rational.COM (Mike Cuddy)</td></tr><tr><td><b>Date: </b>03/12/1992, 14:47</td></tr></table><table border=0 cellspacing=0 cellpadding=0 width="100%" class="moz-header-part2 moz-main-header"><tr><td><b>Newsgroups: </b>rec.games.abstract</td></tr></table><br>
<div class="moz-text-plain"><pre wrap class="moz-quote-pre">
<a class="moz-txt-link-abbreviated" href="mailto:dbh@doc.ic.ac.uk">dbh@doc.ic.ac.uk</a> (Denis Howe) writes:

</pre><blockquote type=cite><blockquote type=cite><blockquote type=cite><pre wrap class="moz-quote-pre">
I have never heard of this Poison Cookie before.  Is it published?
</pre></blockquote></blockquote></blockquote><pre wrap class="moz-quote-pre">

</pre><blockquote type=cite><blockquote type=cite><pre wrap class="moz-quote-pre">
I'll see if I can find it in my book collection.
</pre></blockquote></blockquote><pre wrap class="moz-quote-pre">

Here's what I found (BASIC program included at bottom for humor value!)

 From David H. Ahl's <span class="moz-txt-underscore"><span class="moz-txt-tag">_</span>Basic_Computer_Games<span class="moz-txt-tag">_</span></span> (Microcomputer Edition), 
Creative Computing Press, 1979 (ISBN: 0-916688-07-0), (without permission):

P. 44:

CHOMP
-----------------

    This program is an adaption of a mathematical game originally described by 
Martin Gardner in the January 1973 issue of ``Scientific Amierican''.  Up to a
9x9 grid is set up for you with the upper left square a poison square.  This 
grid is the cookie.  Players alternately chomp away at the cookie from the
lower right.  To take a chomp, input a row and column number of one of the
squares remaining on the cookie.  All of the squares below and to the 
right of that square, including that square, disappear. 
    Any number of people can play--the computer is only the moderator; it is
not a player. Two-person strategies are interesting to work out, but strategies
when there are three or more people are playing are a real challenge.
    The computer version of the game was written by Peter Sessions of the 
People's Computer Company.

10 PRINT TAB(33);"CHOMP"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
30 PRINT:PRINT:PRINT
40 DIM A(10,10)
100 REM ** THE GAME OF CHOMP *** COPYRIGHT PCC 1973 ***
110 PRINT
120 PRINT "THIS IS THE GAME OF CHOMP (SCIENTIFIC AMERICAN, JAN 1973)"
130 PRINT "WANT THE RULES (1=YES, 2=NO!)";
140 INPUT R
150 IF R=0 THEN 340
160 F=1
170 R=5
180 C=7
190 PRINT "CHOMP IS FOR 1 OR MORE PLAYERS (HUMANS ONLY)."
200 PRINT
210 PRINT "HERE'S HOW A BOARD LOOKS (THIS ONE IS 5 BY 7):"
220 GOSUB 540
230 PRINT "THE BOARD IS A BIG COOKIE - R ROWS HIGH AND C COLUMNS"
250 PRINT "WIDE.  YOU INPUT R AND C AT THE START. IN THE UPPER LEFT"
260 PRINT "CORNER OF THE COOKIE IS A POISON SQUARE (P).  THE ON WHO"
270 PRINT "CHOPMPS THE POISON SQUARE LOSES. TO TAKE A CHOMP, TYPE THE"
280 PRINT "ROW AND COLUMN OF ONE OF THE SQUARES ON THE COOKIE."
290 PRINT "ALL OF THE SQUARES BELOW AND TO THE RIGHT OF THAT SQUARE"
300 PRINT "(INCLUDING THAT SQUARE, TOO) DISAPPEAR -- CHOMP!!"
310 PRINT "NO FAIR CHOMPING SQUARES THAT HAVE ALREADY BEEN CHOMPED,"
320 PRINT "OR THAT ARE OUTSIDE THE ORIGINAL DIMENSIONS OF THE COOKIE."
330 PRINT
340 PRINT "HERE WE GO..."
350 REM
360 F=0
370 FOR I=1 TO 10
372 FOR J=1 TO 10
375 A(I,J)=0
377 NEXT J
379 NEXT I
380 PRINT
390 PRINT "HOW MANY PLAYERS";
400 INPUT P
410 I1=0
420 PRINT "HOW MANY ROWS";
430 INPUT R
440 IF R&lt;=9 THEN 470
450 PRINT "TOO MANY ROWS (9 IS MAXIMUM). NOW, ";
460 goto 420
470 PRINT "HOW MANY COLUMNS";
480 INPUT C
490 IF C&lt;=9 THEN 530
500 PRINT "TOO MANY COLUMNS. (9 IS MAXIMUM). NOW, ";
510 GOTO 470
530 PRINT
540 FOR I=1 TO R
550 FOR J=1 TO C
560 A(I,J)=1
570 NEXT J
580 NEXT I
590 A(1,1) = -1
600 REM PRINT THE BOARD
610 PRINT
620 PRINT TAB(7);"1 2 3 4 5 6 7 8 9"
630 FOR I=1 TO R
640 PRINT I;TAB(7);
650 FOR J=1 TO C
660 IF A(I,J)=-1 THEN 700
670 IF A(I,J)=0 THEN 720
680 PRINT "* ";
690 GOTO 710
700 PRINT "P ";
710 NEXT J
720 PRINT
730 NEXT I
740 PRINT
750 IF F=0 THEN 770
760 RETURN
770 REM GET CHOMPS FOR EACH PLAYER IN TURN
780 LET I1=I1+1
790 LET P1=I1-INT(I1/P)*P
800 IF P1&lt;&gt;0 THEN 820
810 P1=P
820 PRINT "PLAYER ";P1
830 PRINT "COORDINATES OF CHOMP (ROW,COLUMN)";
840 INPUT R1,C1
850 IF R1&lt;1 THEN 920
860 IR R1&gt;R THEN 920
870 IF C1&lt;1 THEN 920
880 IF C1&gt;C THEN 920
890 IF A(R1,C1)=0 THEN 920
900 IF A(R1,C1)=-1 THEN 1010
910 GOTO 940
920 PRINT "NO FAIR. YOU'RE TRYING TO CHOMP ON EMPTY SPACE!"
930 GOTO 820
940 FOR I=R1 TO R
950 FOR J=C1 TO C
960 A(I,J)=0
970 NEXT J
980 NEXT I
990 GOTO 610
1000 REM END OF GAME DETECTED IN LINE 900
1010 PRINT "YOU LOSE, PLAYER ";P1
1020 PRINT
1030 PRINT "AGAIN (1=YES; 0=NO!)";
1040 INPUT R
1050 IF R=1 THEN 340
1060 END


--Mike Cuddy (<a class="moz-txt-link-abbreviated" href="mailto:mcuddy@rational.com">mcuddy@rational.com</a>, <a class="moz-txt-link-abbreviated" href="mailto:fensende!mcuddy@apple.com">fensende!mcuddy@apple.com</a>)
To Anachreon in Heav'n/Where he sat in full glee/A few sons of harmony sent a 
petition/That he thier inspierer and patron would be/When this answer arrived/
from the jolly old Grecian/"Voice, fiddle and flute,/no longer be mute,/
I'll lend ye my name/And inspire ye ta' boot/And besides I'll instruct ye/
like me, to entwine/the Myrtle of Venus with Bacchus' vine!
-- sing to the tune of the star spangled banner... (these are the orig words,
yes folks, our national anthem is a ``let's get drunk and F*** song'').

</pre></div></body>
</html>
</table></div>